@ on compile
@ require laser:global
@ require laser:translations

define function tick {
    @ tag tick
    
    as @a function {
        var choosing_language = resource</choosing_language>
        # Choosing Language Message
        define function choosing_language {
            # generate a text component
            private var tc = [""]
            eval tc.add({translate: "message.choose_language", color:"gray"})
            private var i = 0
            for (language in languages){
                eval i += 1
                eval tc.add("\n")
                eval tc.add({
                    translate: "message.choose_language.item_format", color:"gray", with:[{
                        translate: "[%s]", 
                        color:"green",
                        clickEvent:{
                            action:"run_command", 
                            value:"/trigger language set " + (i*-1)
                        },
                        with:[{
                            translate: language["language.name"]
                        }]
                    }]
                })
            }
            eval tc.add("\n")
            eval tc.add({translate: "message.choose_language.help_translate_offset", color: "gray", with:[{
                    translate: "[%s]", 
                    color:"gold",
                    clickEvent:{
                        action:"open_url", 
                        value:TRANSLATIONS_URL
                    },
                    with:[{
                        translate: "message.choose_language.help_translate"
                    }]
                }]
            })
            # print the text component
            function ${message(tc)}
        }
        
        
        # Choose the language
        unless score @s language matches 0 function {
            if score @s language matches ${new int_range(languages.length*-1, -1)} function {
                define function chosen {
                    set @s -> chosen_language = @s -> language
                    if entity @s[tag=laser.chosen_language] function ${message([
                            {translate:"message.changed_language",color:"gray"},
                            "\n",
                            {translate:"message.trigger",color:"gray",with:[{text:"[/trigger mining_laser]",color:"green",clickEvent:{action:"run_command",value:"/trigger mining_laser"}}]}
                        ])}
                    unless entity @s[tag=laser.chosen_language] function {
                        function ${message([
                            {translate:"message.choosen_language",color:"gray"},
                            "\n",
                            {translate:"message.trigger",color:"gray",with:[{text:"[/trigger mining_laser]",color:"green",clickEvent:{action:"run_command",value:"/trigger mining_laser"}}]}
                        ])}
                    }
                    tag @s add laser.chosen_language
                }
                unless entity @s[tag=laser.chosen_language] function /chosen
                unless score @s chosen_language = @s language function /chosen
            }
            if score @s language matches 1.. function $choosing_language
            set @s -> language = 0
        }
        scoreboard players enable @s language
        
        # New players
        if entity @s[tag=!laser.init] function {
            set @s -> chosen_language = -1
            if score @s chosen_language matches -1
                tag @s add laser.init
            function $choosing_language
        }
        # Rejoined players
        if score @s rejoin matches 1.. function {
            scoreboard players reset @s rejoin
            unless entity @s[tag=laser.chosen_language] function $choosing_language
        }
        
        # Guide Book Giving
        unless score @s mining_laser matches 0 function {
            scoreboard players set @s mining_laser 0
            function ${givebook({translate:"guide_book.title",italic:false}, GUIDE_BOOK)}
        }
        scoreboard players enable @s mining_laser
        
        # Blast Furnace Detect
        if score @s used.bfurnace matches 1.. at @s anchored eyes positioned ^ ^ ^ anchored feet function {
            scoreboard players reset @s used.bfurnace
            set hit -> temp = 0
            set distance -> temp = 0
            summon area_effect_cloud{Tags:["blast_furnace_detect"]}
            function {
                teleport @e[tag=blast_furnace_detect,distance=..2] ~ ~ ~
                if block ^ ^ ^ blast_furnace function {
                    set hit -> temp = 1
                    align xyz positioned ~.5 ~.5 ~.5 unless entity @e[type=$blast_furnace,distance=..0.001] function {
                        if block ~ ~ ~ blast_furnace[facing=north] summon $blast_furnace{Tags:["north"],Rotation:[0f,0f]}
                        if block ~ ~ ~ blast_furnace[facing=east]  summon $blast_furnace{Tags:["east"], Rotation:[90f,0f]}
                        if block ~ ~ ~ blast_furnace[facing=south] summon $blast_furnace{Tags:["south"],Rotation:[180f,0f]}
                        if block ~ ~ ~ blast_furnace[facing=west]  summon $blast_furnace{Tags:["west"], Rotation:[-90f,0f]}
                    }
                }
                scoreboard players add distance temp 1
                if score hit temp matches 0 if score distance temp matches ..5000 positioned as @e[tag=blast_furnace_detect,distance=..2] positioned ^ ^ ^0.001 function /
            }
        }
    }
    
    
    # Laser Working
    var laser = resource</laser>
    define function laser {
        set burn_time -> temp = (~ ~ ~).BurnTime
        if score @s remove_burn_time matches 1.. function {
            if score burn_time temp < @s remove_burn_time function {
                scoreboard players operation @s remove_burn_time -= burn_time temp
                set burn_time -> temp = 0
                set (~ ~ ~).BurnTime = 0
            }
            if score burn_time temp >= @s remove_burn_time function {
                scoreboard players operation burn_time temp -= @s remove_burn_time
                set @s -> remove_burn_time = 0
                set (~ ~ ~).BurnTime = burn_time -> temp
            }
            
        }
        if entity @s[tag=activating] function {
            tag @s add laser
            tag @s remove activating
            replaceitem block ~ ~ ~ container.0 air 1
            data modify block ~ ~ ~ Items prepend from entity @e[tag=temporary_saving_storage,limit=1] Items[{Slot:0b}]
            kill @e[tag=temporary_saving_storage]
        }
        if data block ~ ~ ~ Items[{Slot:0b,id:"minecraft:diamond_pickaxe"}] unless data block ~ ~ ~ Items[{Slot:0b,id:"minecraft:diamond_pickaxe",tag:{Damage:1560}}] function {
            if score burn_time temp matches 1.. function {
                tag @s add laser
                scoreboard players add @s working 1
                
                if score @s working matches ..59 particle smoke ^ ^1 ^2 0.35 0.35 0.35 0 1
                if score @s working matches 60.. particle dust 1 0 0 1 ^ ^1 ^2 0.35 0.35 0.35 0 1
                
                if score @s working matches 60.. function {
                    positioned ^ ^ ^1 function {
                        var modifier = function (item_, name, scale) {
                            do if (scale == null)
                                eval scale = 1
                            if data block ~ ~ ~ Items[{id:${new nbt(item_)}}] function {
                                var i = null
                                for (i = 0; i < 5; i += 1) {
                                    set count -> temp = (~ ~ ~).Items[{id:${new nbt(item_)},Slot:${new tag_byte(i)}}].Count * ${(real) scale}
                                    scoreboard players operation $name temp += count temp
                                }
                            }
                        }
                        
                        set max_distance -> temp = $DEFAULT_DISTANCE
                        eval modifier("minecraft:diamond", "max_distance", 2)
                        eval modifier("minecraft:emerald", "max_distance", 2)
                        eval modifier("minecraft:diamond_block", "max_distance", 18)
                        eval modifier("minecraft:emerald_block", "max_distance", 18)
                        
                        set speed -> temp = 0
                        eval modifier("minecraft:redstone", "speed")
                        eval modifier("minecraft:redstone_block", "speed", 9)
                        
                        execute store result score 3x3 temp if data block ~ ~ ~ Items[{id:"minecraft:nether_star"}]
                    }
                    # https://www.desmos.com/calculator/docke3ni2w
                    scoreboard players operation speed temp /= 20 const
                    scoreboard players add speed temp 25
                    set period -> temp = 4460
                    scoreboard players operation period temp /= speed temp
                    scoreboard players remove period temp 25
                    
                    var raycast = resource</raycast>
                    define function raycast {
                        if score distance temp matches 61.. function {
                            if entity @s[tag=forward] function {
                                if entity @s[tag=north] particle dust 1 0 0 1 ~ ~ ~ 0 0 0.25 0 1
                                if entity @s[tag=south] particle dust 1 0 0 1 ~ ~ ~ 0 0 0.25 0 1
                                if entity @s[tag=east] particle dust 1 0 0 1 ~ ~ ~ 0.25 0 0 0 1
                                if entity @s[tag=west] particle dust 1 0 0 1 ~ ~ ~ 0.25 0 0 0 1
                            }
                            if entity @s[tag=up] particle dust 1 0 0 1 ~ ~ ~ 0 0.25 0 0 1
                            if entity @s[tag=down] particle dust 1 0 0 1 ~ ~ ~ 0 0.25 0 0 1
                        }
                        
                        if score distance temp matches ..60 function {
                            set laser_id -> temp = @s -> laser_id
                            if entity @s[tag=forward] as @e[tag=laser_end] if score @s laser_id = laser_id temp tp @s ^ ^-.4 ^-1
                            if entity @s[tag=down] as @e[tag=laser_end] if score @s laser_id = laser_id temp tp @s ^ ^.005 ^-.6
                            if entity @s[tag=up] as @e[tag=laser_end] if score @s laser_id = laser_id temp tp @s ^ ^.005 ^-1.5
                        }
                        
                        var destroy_block = resource</destroy_block>
                        define function destroy_block {
                            unless block ~ ~ ~ #laser:ignore unless block ~ ~ ~ #laser:undestroyable function {
                                if block ~ ~ ~ #laser:destroy setblock ~ ~ ~ air destroy
                                unless block ~ ~ ~ #laser:air unless block ~ ~ ~ #laser:destroy function {
                                    summon armor_stand{Marker:1b,Tags:["miner"]} ~ -3 ~
                                    at @s data modify entity @e[tag=miner,limit=1] HandItems[0] set from block ~ ~ ~ Items[{Slot:0b}]
                                    as @e[tag=miner] function {
                                        loot spawn ~ ~ ~ mine ~ ~ ~ mainhand
                                        kill @s
                                    }
                                    setblock ~ ~ ~ air
                                }
                                align xyz tag @e[type=item,dx=0,dy=0,dz=0] add laser_item
                            }
                        }
                        /forceload add ~-1 ~-1 ~1 ~1
                        unless block ~ ~ ~ #laser:ignore function {
                            set hit -> temp = 1
                            unless block ~ ~ ~ #laser:undestroyable function {
                                set remove_burn_time -> temp = $BURN_TIME_FOR_BLOCK
                                scoreboard players operation remove_burn_time temp /= period temp
                                scoreboard players operation @s remove_burn_time += remove_burn_time temp
                                scoreboard players add @s power 1
                                at @s function {
                                    set (~ ~ ~).CookTime = @s -> power
                                    set (~ ~ ~).CookTimeTotal = period -> temp
                                }
                                particle dust 1 1 1 1 ~ ~ ~ 0.25 0.25 0.25 0.2 1
                                if score @s power >= period temp function {
                                    scoreboard players operation @s power -= period temp
                                    if block ~ ~ ~ #laser:destroy setblock ~ ~ ~ air destroy
                                    unless block ~ ~ ~ #laser:air unless block ~ ~ ~ #laser:destroy function {
                                        summon armor_stand{Marker:1b,Tags:["miner"]} ~ -3 ~
                                        at @s data modify entity @e[tag=miner,limit=1] HandItems[0] set from block ~ ~ ~ Items[{Slot:0b}]
                                        as @e[tag=miner] function {
                                            loot spawn ~ ~ ~ mine ~ ~ ~ mainhand
                                            kill @s
                                        }
                                        setblock ~ ~ ~ air
                                    }
                                    if score 3x3 temp matches 1.. function {
                                        positioned ^1  ^   ^ function $destroy_block
                                        positioned ^1  ^1  ^ function $destroy_block
                                        positioned ^   ^1  ^ function $destroy_block
                                        positioned ^-1 ^1  ^ function $destroy_block
                                        positioned ^-1 ^   ^ function $destroy_block
                                        positioned ^-1 ^-1 ^ function $destroy_block
                                        if entity @s[tag=down] if score distance temp matches 1.. positioned ^ ^-1 ^ function $destroy_block
                                        unless entity @s[tag=down] positioned ^ ^-1 ^ function $destroy_block
                                        positioned ^1  ^-1 ^ function $destroy_block
                                    }
                                    playsound block.fire.extinguish block @a ~ ~ ~ 0.25 1.3
                                    at @s function {
                                        summon area_effect_cloud ~ ~ ~ {Tags:["rng"]}
                                        as @e[tag=rng,distance=..1] function {
                                            set rng -> temp = @s.UUIDMost * 0.00000000023283064365386962890625
                                            kill @s
                                        }
                                        scoreboard players operation rng temp %= 100 const
                                        scoreboard players add rng temp 1
                                        set level -> temp = (~ ~ ~).Items[{Slot:0b}].tag.Enchantments[{id:"minecraft:unbreaking"}].lvl
                                        scoreboard players add level temp 1
                                        set damage -> temp = (~ ~ ~).Items[{Slot:0b}].tag.Damage
                                        set chance -> temp = 100
                                        scoreboard players operation chance temp /= level temp
                                        if score chance temp >= rng temp scoreboard players add damage temp 1
                                        set (~ ~ ~).Items[{Slot:0b}].tag.Damage = damage -> temp
                                    }
                                }
                            }
                        }
                        /forceload remove ~-1 ~-1 ~1 ~1
                        
                        align xyz as @e[dx=0,dy=0,dz=0] function {
                            /execute unless entity @s[type=#laser:ignored] run data merge entity @s {Fire:60s}
                            effect give @s[type=player] wither 2 0 true
                            tag @s[type=item] add laser_item
                            tag @s[type=experience_orb] add laser_xp_orb
                        }
                        if entity @e[tag=laser_xp_orb] at @s function {
                            var loop = resource</>
                            tag @e[tag=laser_xp_orb,limit=1] add target
                            set value -> temp = @e[tag=target,limit=1].Value * 2
                            if data block ~ ~ ~ Items[{Slot:0b,tag:{Enchantments:[{id:"minecraft:mending"}]}}] function {
                                set damage -> temp = (~ ~ ~).Items[0].tag.Damage
                                if score damage temp >= value temp function {
                                    scoreboard players operation damage temp -= value temp
                                    set (~ ~ ~).Items[0].tag.Damage = damage -> temp
                                    set value -> temp = 0
                                }
                                if score damage temp < value temp function {
                                    set (~ ~ ~).Items[0].tag.Damage = 0
                                    scoreboard players operation value temp -= damage temp
                                }
                            }
                            if score value temp matches 2.. positioned ^ ^-.2 ^-.65 function {
                                if entity @s[tag=north] summon experience_orb{Tags:["output"],Motion:[0.0,0.0,-0.15]}
                                if entity @s[tag=east]  summon experience_orb{Tags:["output"],Motion:[0.15,0.0,0.0]}
                                if entity @s[tag=south] summon experience_orb{Tags:["output"],Motion:[0.0,0.0,0.15]}
                                if entity @s[tag=west]  summon experience_orb{Tags:["output"],Motion:[-0.15,0.0,0.0]}
                                set @e[tag=output,distance=..0.5,limit=1].Value = value -> temp * 0.5
                                tag @e[distance=..0.5] remove output
                            }
                            at @e[tag=target] playsound entity.chicken.egg block @a ~ ~ ~ 0.25 0.7
                            kill @e[tag=target]
                            if entity @e[tag=laser_xp_orb] function $loop
                        }
                        if entity @e[tag=laser_item] at @s positioned ^ ^-.2 ^-.65 function {
                            var loop = resource</>
                            tag @e[tag=laser_item,limit=1] add cloning
                            if entity @s[tag=north] summon item{Tags:["output"],Item:{id:"stone",Count:1b},Motion:[0.0,0.0,-0.15]}
                            if entity @s[tag=east]  summon item{Tags:["output"],Item:{id:"stone",Count:1b},Motion:[0.15,0.0,0.0]}
                            if entity @s[tag=south] summon item{Tags:["output"],Item:{id:"stone",Count:1b},Motion:[0.0,0.0,0.15]}
                            if entity @s[tag=west]  summon item{Tags:["output"],Item:{id:"stone",Count:1b},Motion:[-0.15,0.0,0.0]}
                            set @e[tag=output,limit=1,distance=..0.5].Item = @e[tag=cloning,limit=1].Item
                            tag @e[distance=..0.5] remove output
                            at @e[tag=cloning] playsound entity.chicken.egg block @a ~ ~ ~ 0.5 0.7
                            kill @e[tag=cloning]
                            if entity @e[tag=laser_item] function $loop
                        }
                        
                        scoreboard players add distance temp 1
                        if score hit temp matches 0 if score distance temp <= max_distance temp positioned ^ ^ ^1 function /
                    }
                    set hit -> temp = 0
                    set distance -> temp = 0
                    positioned ^ ^1 ^2 function {
                        if entity @s[tag=forward] positioned ^ ^ ^1 function $raycast
                        if entity @s[tag=down] rotated ~ 90 positioned ^ ^ ^1 function $raycast
                        if entity @s[tag=up] rotated ~180 -90 positioned ^ ^ ^1 function $raycast
                    }
                }
                
                if score @s working matches 60 function {
                    set laser_id -> temp = @s -> laser_id
                    as @e[tag=laser_start] if score @s laser_id = laser_id temp function {
                        tp @s ^ ^.6 ^2
                        data merge entity @s {NoAI:1b,Team:"no_collision"}
                        effect give @s invisibility 1000000 0 true
                    }
                    as @e[tag=laser_end] if score @s laser_id = laser_id temp function {
                        tp @s ^ ^.6 ^2
                        data merge entity @s {Invulnerable:1b}
                        effect give @s invisibility 1000000 0 true
                    }
                    stopsound @a * entity.guardian.attack
                    playsound block.beacon.activate block @a ^ ^1 ^2 0.8 1.2
                }
            }
            if score burn_time temp matches 0 if data block ~ ~ ~ Items[{Slot:1b}] function {
                tag @s add laser
                tag @s add activating
                summon chest_minecart ~ -3 ~ {NoGravity:1b,Tags:["temporary_saving_storage"]}
                data modify entity @e[tag=temporary_saving_storage,limit=1] Items append from block ~ ~ ~ Items[{Slot:0b}]
                replaceitem block ~ ~ ~ container.0 iron_ore{Activate:1b}
            }
        }
    }
    
    # Blast Furace Working
    as @e[type=$blast_furnace] at @s function {
        tag @s remove laser
        unless block ~ ~ ~ blast_furnace kill @s
        if block ~ ~1 ~ redstone_lamp[lit=true] if block ~ ~ ~ blast_furnace if block ^ ^1 ^1 iron_block function {
            for (direction in directions) {
                if entity @s[tag=$direction] if block ^ ^ ^1 hopper[facing=$direction] function {
                    if block ^ ^1 ^2 observer[facing=$direction] function {
                        tag @s add forward
                        function $laser
                        tag @s remove forward
                    }
                    if block ^ ^1 ^2 observer[facing=up] function {
                        tag @s add down
                        function $laser
                        tag @s remove down
                    }
                    if block ^ ^1 ^2 observer[facing=down] function {
                        tag @s add up
                        function $laser
                        tag @s remove up
                    }
                }
            }
        }
        # Enabling
        if entity @s[tag=laser] unless entity @s[tag=worked_last_tick] function {
            tag @s add worked_last_tick
            summon guardian ~ -3 ~ {PersistenceRequired:1b,Silent:1b,Invulnerable:1b,Tags:["laser","laser_start"]}
            summon squid ~ -2 ~3 {PersistenceRequired:1b,Silent:1b,Tags:["laser","laser_end"],Team:"no_collision"}
            scoreboard players add id laser_id 1
            as @e[tag=laser] unless score @s laser_id matches 1.. set @s -> laser_id = id -> laser_id
            positioned ^ ^1 ^2 function {
                for (direction in all_directions) {
                    fill ~ ~ ~ ~ ~ ~ observer[powered=true,facing=$direction] replace observer[powered=false,facing=$direction]
                }
            }
        }
        
        # Disabling
        if entity @s[tag=worked_last_tick] unless entity @s[tag=laser] function {
            scoreboard players reset @s working
            tag @s remove worked_last_tick
            set laser_id -> temp = @s -> laser_id
            as @e[tag=laser_start] if score @s laser_id = laser_id temp function {
                tp @s ~ -5 ~
                kill @s
            }
            as @e[tag=laser_end] if score @s laser_id = laser_id temp function {
                tp @s ~ -5 ~
                kill @s
            }
            scoreboard players reset @s laser_id
            positioned ^ ^1 ^2 function {
                for (direction in all_directions) {
                    fill ~ ~ ~ ~ ~ ~ observer[powered=false,facing=$direction] replace observer[powered=true,facing=$direction]
                }
            }
        }
    }
    
    clear @a iron_ore{Activate:1b}
    
    as @e[tag=laser_start] data remove entity @s Motion
    as @e[tag=laser_end] data remove entity @s Motion
}
